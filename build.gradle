plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
    id 'io.freefair.lombok' version '6.0.0-m2'
}

group 'com.klushcheva'
version = '1.0'

def junitVersion = "5.9.3",
    restAssuredVersion = "5.3.1",
    jsonSchemaValidatorVersion = "5.3.1",
    allureRestAssuredVersion = "2.23.0",
    ownerVersion = "1.0.12"

allure {
    allureReport {
        version.set ("2.25.0")
    }
    adapter {
        aspectjWeaver.set(true)
        aspectjVersion.set("1.9.21")
        frameworks {
            junit5 {
                adapterVersion.set("2.25.0")
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation (
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$jsonSchemaValidatorVersion",
            "io.qameta.allure:allure-rest-assured:$allureRestAssuredVersion",
            "org.aeonbits.owner:owner:$ownerVersion"
    )
}

tasks.withType(Test).configureEach {
    systemProperties(System.getProperties())
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test {
    useJUnitPlatform()
}

tasks.register('smoke_test', Test) {
    useJUnitPlatform {
        includeTags("Smoke")
    }
}

tasks.register('regress_test', Test) {
    useJUnitPlatform {
        includeTags("Regress")
    }
}